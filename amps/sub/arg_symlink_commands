#!/usr/bin/perl -n
# arg_symlink_cmds: output bash code to create symlinks for named arguments

use strict;
use warnings;

our ($template, $count, @names, @symlink_commands);

BEGIN {
	$template = <<'End';
[ -n "$file" ] && ln -s "$(readlink -f "$file")" "$work_dir/$arg_name"
End
	$count = 0;
	@names = ();
	@symlink_commands = ();
}

# argument number and name
my $arg_num = $.;
chomp(my $arg_name = $_);

# replace $file and $arg_name in the template
my $command = $template;
$command =~ s/\$file/\${$arg_num}/g;
$command =~ s/\$arg_name/$arg_name/g;

# collect the symlink commands
push @symlink_commands, $command;
++$count;

# collect argument names
push @names, $arg_name;

# finally, code to check that the right number of arguments were provided, then output the symlink commands
END {
	print <<End;
[ \$# = $count ] || { echo >&2 usage: "\$prog @names"; exit 2; }
End
	print @symlink_commands;
}
